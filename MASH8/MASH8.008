;C:\Documents and Settings\Gergana\Desktop\CrystalForth\TEMPFO\mash8.asm
; -----------------------------------------------
; _DUP	\  STACK
; _J	 \
; _PUSH	 /
; _POP	/
; 
; _@R+	\  TRANSFER
; _!R+	 \
; _RX	 /	RA XA
; _TX	/	RT XCHG
; 
; _EXIT	\  CONTROL
; _JUMP	 \
; _NEXT	 /
; _IF	/
; 
; _+*	\  MATH AND LOGIC 
; _+2/	 \
; _-/	 /
; _NAND	/
; 

locals  @@

	     pd	 equ dword ptr
	     pw	 equ word  ptr
	     pb	 equ byte  ptr

TO_ =	-2
AT_ =	-4
VL_ =	 2
qqq_= 0

JMP_    MACRO LBL
	jmp   SHORT LBL
    ENDM

SKIPA_   MACRO
	DB    03DH
    ENDM

SKIPB_   MACRO
	DB    03CH
    ENDM

SKIPR_   MACRO reg
	mov   reg,0
	org   $-2
    ENDM

X_  MACRO
	xchg  Rsp,DSP
  ENDM

TOD_    MACRO  REG
	PUSH  REG
    ENDM

DTO_    MACRO  REG
	POP   REG
    ENDM

TWICE_    macro
	DW	$+2
    endm

RDEC_    macro
	DW	@_NEXT_
	TWICE_
    endm

;@I2P   macro
;	MOV WR,[I]
;	I2P
;	TEST	AL,1
;    endm

DROP_    macro
  XCHG  T,W
	DTO_	T
  endm

DUPW_    macro
	TOD_	T
  XCHG  T,W
  endm

CLR_   macro     REG
	XOR   REG,REG
  endm

?ZERO_   MACRO   REG
	OR	REG,REG
	ENDM

RTO_     MACRO   REG
	X_
	POP	REG
	X_
    ENDM

TOR_     MACRO   REG
	X_
	PUSH	REG
	X_
  ENDM

LDWP_ MACRO  
	LODSW
  ENDM

STWP_ MACRO   
	STOSW
  ENDM

ALIGN_  MACRO  FLAG
	IF ($-START+FLAG) AND 1
    NOP
qqq_ = qqq_+1    
	ENDIF
	ENDM

N_	MACRO LBL	
LBL:
    ENDM

L_	MACRO LBL
	ALIGN_  0
LBL:
ENDM

P_  MACRO LBL
	ALIGN_  0
	NOP
LBL:
	org   $-1
  ENDM

@nam    macro   msg
	local	endstr
	db   	endstr-$-1
	db   	msg
endstr	label   byte
	endm

@ln     macro   token
ppppp = $
	@nam  token
	dw    qqqqq
qqqqq = ppppp
	endm

XT_     macro   LBL,ADR
  L_    LBL
  DW    ADR
  ENDM

VAR_    macro   LBL,DAT
  XT_   LBL,VAR@
  DW    DAT
  ENDM

const_  macro   lbl,dat
  XT_   LBL,@_CONS@
  DW    DAT
  endm

DEFER_  macro   lbl,dat
  XT_   LBL,@_JUMP_
  DW    DAT
  endm

value_  macro   lbl,dat
	ALGN_  0
  DW    @_SETVA
  const_    LBL,dat
  endm

vector_ macro   lbl,dat
	ALGN_  0
  DW    @_SETVA
  DEFER_    LBL,DAT
  endm

point_  macro   lbl1,LBL2,dat
	L_  LBL1
  DW  @_POP__
  vector_ LBL2,dat
  endm

QUAN_   MACRO   LBL,DAT
	ALGN_  0
  DW    VAR3@
  vector_    lbl,DAT
  ENDM

entry_  macro ADR,DAT,nam
  DW    ADR,DAT
  @nam  nam
  endm

        
        
        
        
        
; REGISTERS     for interpretter
  I	  = SI	; INSTUCTION POINTER
  RSP = BP	; RETURN	STACK POINTER
  DSP = SP	; DATA STACK POINTER 
	RT  = DI	; RETURN	STACK TOP REGISTER

  T   = CX  ; DATA STACK TOP REGISTER
  TL  = CL
  TH  = CH
  W   = AX  ; WORK REGISTER
  WL	= AL
  WH	= AH
  AR  = BX  ; ADDRESS & ARITHMETHIC REGISTER
	
	; AX CX DX BX SCRATCH
	; KEEP I DSP RSP RT

  NACC 	=	DI
	NBASE	=	BP

MyCseg  segment para  use16

    assume cs:	MyCseg,	 ds: MyCseg, ss: MyCseg, es: MyCseg

    org 100h

	@TBUF   = -256	     ;	256
	@TIB    = @TBUF  *	2    ;	256 ;	512 B FSTACK
	@R_TOP  = @TIB   *	2    ; 1024 B RSTACK
	@S_TOP  = @R_TOP *	2    ; 2048 B DSTACK
	@VOCTOP = @S_TOP *	2    ; 4096 B =	4 BUFFERS * 1024 B

    Start   Label byte
@_@START:
	mov     bX,1000h	  ; only 64k allocate
	mov     ah,4ah
	int     21h
 P_	@_ABORT
	CLD
	MOV     DSP,@S_TOP
	MOV     RSP,@R_TOP
	CALL    _EXECU
@_@INITA:	; INIT TIB CSTR
  DW    0
  
;--------------- RETURN STACK --------------------
	
 P_ @_POP__         ; POP
  TOD_  T
  MOV   T,RT
  JMP_  @RDROP

 P_ @_IOZ
  TOD_  T
 P_ @_IO1
  TOD_  T
 P_ @_IO2
  TOD_  T
 P_ @_IO3
  TOD_  T
 P_ @_IO4
  X_
  PUSH  AR RT
  X_
  XCHG  AX,T
  POP   DI CX DX
  MOV   BX,DX
  XCHG  BX,I
  JMP   BX
  
; Return input character and true, 
; or a false if no input. 

 L_ @_?CRX      ; (  -- c T | F )  
  DW @_IOZ
  MOV   DX,0FFFFH ; input command & setup for TRUE flag
  MOV   AH,6    ; MS - DOS Direct Console I/O
  INT   021H    ;
  JZ    QRX3    ; ?key ready
  OR    AL,AL   ; AL=0 if extended char
  JNZ   QRX1    ; ?extended character code
  INT   021H    ;
  STC
QRX1:
  MOV   AH,0
  JNC   QRX2
  XCHG  AH,AL   ; extended code in msb
QRX2:
  PUSH  AX      ; save character 
  SKIPB_
QRX3:
  DEC   DX      
  PUSH  DX      ; flag
  JMP_  @_@IORET
  
 L_ @_CTX      ; ( c -- ) \ Send character c to output device.
  DW  @_IO1     ; char in DL
  CMP   DL,0FFH ; 0FFH is interpreted as input
  JNZ   @TX1    ; do NOT allow input
  SKIPA_
  
 L_ @_SPACE 
  DW @_IOZ
  MOV   DL,32   ; change to blank
@TX1:
  MOV   AH,6    ; MS - DOS Direct Console I/O
  INT   021H    ; display character
  
@_@IORET:  
  X_
  POP   RT AR 
  X_
 
 P_ @_DROPX
  DTO_  T
  
 P_ @_RET           ; RET
  MOV   I,RT
  
 P_ @_RDROP         ; RDROP
@RDROP: 
  RTO_  RT
  JMP_	_MAIN
  
; --------------------  CONTROL  -------------------
	
 P_	@_IFM 					  ; (IFM
  DEC   T
  JS	  _SKIP

 P_	@_JUMP				    ; (JUMP
_JMP:
  MOV   I,[I]
  JMP_	_MAIN
	
 P_	@_IF  				    ; (IF
  INC   T
  DEC   T
  DTO_  T
  JZ  _JMP
 P_ @_SKIP            ; (SKIP
_SKIP:
	INC		I
	INC		I
  JMP_	_MAIN
	
; -------------------- MATH ------------------- 

 P_	@_PLAVG					; +2/
  DTO_  W
	ADD   T,W
	MOV   W,T
	RCR   T,1
	TOD_	W 
  JMP_	_MAIN

 P_	@_PMUL					; +*
  DTO_  W
	TEST  WL,1
	JZ    @D2DIV
	ADD   T,AR
@D2DIV:				     
	RCR   W,1
	RCR   T,1
	TOD_	W 
  JMP_	_MAIN

 P_	@_SDIV					; -/
  DTO_  W
	SHL   W,1
	RCL   T,1
	CMP   W,AR
	JB	  NO_DIV
	SUB   T,AR
	INC   W
NO_DIV:
	TOD_	W 
  JMP_	_MAIN
  
; -------------------------  LOGIC  --------------------	

 P_	@_NAND					; NAND
  DTO_  W
  AND   T,W
 P_ @_NOT           ; NOT
  INC	  T
 P_ @_NEG           ; NEG
  NEG	  T
  JMP_	_MAIN

 P_	@_XOR 					; XOR
  DTO_  W
  XOR   T,W
  JMP_	_MAIN
  
; ---- MAIN LOOP  & STACK  ----------------

 P_ @_DUP           ; DUP
  TOD_  T
  SKIPB_

 P_ @_XA            ; XA
  XCHG  RT,AR
  JMP_	_MAIN
  
 P_ @_XI            ; EX
  XCHG  RT,I
  JMP_	_MAIN
  
 P_	@_XT				    ; XCHG
  XCHG  T,RT
  JMP_	_MAIN
	
 P_ @_FOR           ; (FOR
  MOV   I,[I]
  
 P_ @_PUSH_					; PUSH
  DROP_
  JMP_	_RPUSH
	
_EXECU: 
  DTO_	W
_NEST:
  XCHG	I,W
_RPUSH:
  XCHG	RT,W
  TOR_  W
_MAIN:					    ; (NOP
	LDWP_	
  TEST  WL,1
  JE	  _NEST
  DEC   W
  JMP		W   
	
;-----------------------  TRANSFER ----------------

 P_	@_J			     	  ; J
  TOD_  T
  MOV   T,[RSP]
  JMP_	_MAIN

 P_	@_RLDP				  ; @R+
  TOD_  T
  MOV   T,[RT]
	              
 P_	@_RSKIP 		    ; RSKIP
@RT2P:  
  INC   RT
@RT1P:  
  INC   RT
  JMP_	_MAIN
  
 P_	@_RSTP				  ; !R+
  DROP_
  MOV   [RT],W
  JMP_  @RT2P
  
 P_	@_RLDBP				  ; C@R+
  TOD_  T
  MOV   TL,[RT]
  CLR_  TH
  JMP_  @RT1P
	              
 P_ @_RSTBP         ; C!R+
  DROP_
  MOV   [RT],WL
 P_	@_RSKIPB		    ; RSKIPB
  JMP_  @RT1P
  
 P_	@_RBACK 		    ; RBACK
  DEC   RT
  DEC   RT
  JMP_	_MAIN
  
;-----------------------------------------------

freemem:
;  DW ENDMEM-FREEMEM-2
 ; entry_  @_ENTRY,0,'=:'
  ;DB 0
EndMem:

; --------------  END  ---------------

@_@END:

    MyCseg  ends
    end  Start

;----------------   NUMBERS	 ---------------------

@ATOI:
  XCHG    AX,SI
	MOV   	AL,10   	; BASE 10 INITIALLY
	CALL  	@@numini
	deC   	CX
	LODSB	
	CALL  	@@SGN
@@GETC:	
	JCXZ  	@@EXNUM2
	deC   	CX
	LODSB	
	CALL  	@@N16
	JMP_  	@@GETC

COMOPT  DW @@NB,@@NB,@@NB,@@KOLI,@@KAVI,@@DIEZ
OPTAB   DB "$&%'^#"
NEWBASE DB 16,8,2 

@@SGN:	
	CMP   	AL,'-'     ; SGN
	JNE   	@@N16
	JCXZ  	@@ERNUM
	POP   	dX
	CALL  	dX         ; return to caller
	NEG   	nacc
@@EXNUM2:
	RET

@@NB:
	SHR		BX,1
	MOV		AL,NEWBASE[BX]
	JMP_	@@SETBAS
@@DIEZ:	
	XCHG  	nacc,AX
@@numini:	
	Xor   	nacc,nacc       ; ZEROING ACCUMULATOR
@@setbas:	
	DEC   	AX
	DEC   	AX
	xor   	ah,ah
	mov   	nbase,ax
	INC   	nbase
	INC   	nbase
	JCXZ  	@@ERNUM
	RET
@@ERNUM:	
	DEC		CX
	SKIPB_
@@ERNUM0:	
	;DEC		SI
	INC   	CX
@@STATE:	
	pop   	dx    ; rdrop
	RET
		
@@NORMAL?:
	MOV		BX,6
@@SCANB1:	
	DEC		BX
	JS		@@SCANB2	
	CMP		AL,OPTAB[BX]
	JNE		@@SCANB1	
  CALL  @@STATE
@@N16:	
  CALL  @@NORMAL?
@@SCANB2:	
	JNE   	@@N5
	JCXZ  	@@ERNUM
	SHL		BX,1
	JMP		COMOPT[BX]
		
@@KOLI:	
	deC   	CX
	LODSB	
	AND	  	AL,31
	JMP_  	@@ACCUM
	
@@N5:	
	cmp   	al,'9'+1
	jc    	@@ton2
	cmp   	al,'A'      ; no case sensivity
	jc    	@@ERNUM0
	sub   	al,7
@@ton2:	
	sub   	al,'0'
	cmp   	ax,nbase
	jnc   	@@ERNUM0
	SKIPA_
@@KAVI:	
	deC   	CX
	LODSB
@@ACCUM:	
	Xchg  	nacc,AX
	mul   	nbase
	Xchg  	nacc,AX
	add   	nacc,ax
	RET	
	
 P_ @_ATOI_
	POP		CX AX
	PUSH	I RSP RT
	CALL	@ATOI
	XCHG  AX,nacc
	POP		RT RSP I
	JMP_ 	_RETVAL

;----------------  PARSE   -----------------

;  CHAR etib ltib   /  START LEN >IN  / >IN 				           
;  DX   CX   BX    		dX    AX  Cx

 P_ @_PARS_
  TOD_  T
	POP		CX DI AX	
	JCXZ 	@@SKIPX
	SUB  	DI,CX	   
	CMP  	AL,' '
	JNE  	@@SKIPX
	REPE 	SCASB
	JE   	@@SKIPX
	DEC  	DI
	INC  	CX
@@SKIPX:
	push  	di      ;  START OF THE SOURCE
	push  	di      ;  START OF THE SOURCE
	JCXZ  	@@WEX
	REPNE 	SCASB
	JNE   	@@WEX
	DEC   	DI
@@WEX:          ; END OF THE SOURCE  IN DI
	POP		W 	    ; DX START OF THE WORD
	SUB   DI,W    ; LENGTH OF THE WORD
_RETVAL:  
	PUSH	DI 
_RETLEN:  
  PUSH  CX	; CX REMAIN LENGTH OF THE BUFFER
  DTO_  T
_MAI:  
	JMP		_MAIN
	
; -----------------------
; searching
; -----------------------
  
 P_ @_FIND_
  DTO_  AX
	XOR		CX,CX
  XCHG  AX,I
  MOV   DI,T
FND: 
  ADD   DI,CX
  LEA 	DI,[DI+4]
  mov 	cl,byte[Di]
  jcxz	_DCX ;not_fnd
  inc 	cx
  PUSH  SI
  repe	cmpsb
  POP   SI
  jNZ 	FND
findN:  	
	MOV		CL,2
	ADD		[DI+2],CX
  SKIPA_
_DCX:
  MOV   DI,SI
  XCHG  AX,I 
  TOD_  DI
	MOV 	T,CX
  JMP  	_MAIN

;======================================================

 L_ @_ODD?
    DW @_DUP
 L_ @_ODD
    DW @_AT_ND,1

 L_ @_AT_ND
    DW @_RLDP,@_RDROP
 L_ @_AND
    DW @_NAND,@_NOT,@_RET
    
 L_ @_ALIGN
    DW @_ODD?
 L_ @_PLUS_
  DW @_PLAVG,@_DROPX
 
 L_ @_XTM2D
        DW @_2DIV_
 L_ @_XTIMS
        DW @_JUMP_,@@LPS
@@LPS0  DW @_PUSH_,@_J____,@_EXECU,@_POP__
@@LPS   DW @_NEXT_,@@LPS0
 L_ @_2EXIT
	DW @_RDROP
 L_ @_EXIT_
	DW @_RDROP
 L_ @_NOP__	
	DW @_RET__
	
 L_ @_PREMV
    DW @_ODD?_
 L_ @_XDROP	
	DW @_EX___
 L_ @_DROP_ 
	DW @_DROPX

 L_ @_CONS@
  DW @_RLDP_,@_RDROP,@_RET__
  
  CONST_  @_TBL__,80H
  CONST_  @_TIB__,-512
  CONST_  @_TBUF_,-256
  CONST_  @_BL___,32
  CONST_  @_M1___,-1
  L_ @_ZEQ__
    DW @_IF___,@_M1___
  CONST_  @_0____,0
         
 L_ @_DOES_
  DW @_POP__,@_XCHG_,@_RET__

 L_ @_SETVA
  DW @_RSKIP,@_RSTP_,@_RDROP,@_RET__
  
  VALUE_  @_HERE_,FREEMEM
  VALUE_  @_ETIB_,0
  VALUE_  @_LTIB_,0
  VALUE_  @_DICT_,@VOCTOP
  VALUE_  @_CNTC_,0

  DEFER_  @_OK___,@_nop__
  DEFER_  @_AXEPT,@_nop__ ;_ACC
  DEFER_  @_NUM__,@_nop__
  DEFER_  @_ERROR,@_drop_; _IDTYP
         
  POINT_  @_REINI,@_INITV,@_@_INI
  POINT_  @_SETPT,@_DOIT?,0

 L_ @_MAKER
  dw @_HNTRY
 L_ @_comar
  DW @_RLDP_,@_RDROP,@_JUMP_,@_comma

 L_ @_XDICT
	DW @_DICT_,@_EX___,TO_+@_DICT_,@_RET__
	
 L_ @_XHERE
	DW @_HERE_,@_EX___,TO_+@_HERE_,@_RET__
	
 L_ @_EVAL_
	DW @_NOP__ 
 L_ @_@EVAL   
	DW @_TOKEN,@_DLDB_,@_IF___,@_DROP_,@_DOIT?,@_JUMP_,@_@EVAL
	
 L_ @_SEMIC 
	DW @_comar,@_RET__  		; (;,

 L_ @_clit_   
	dw @_comar,@_lit__       ; (LIT,

 L_ @_NUMBR
	DW @_CSTR_,@_NUM__,@_NZ??_,@_RET__
	
 L_ @_XZ??_
	DW @_EX___
 L_ @_Z??__
	DW @_ZEQ__
 L_ @_NZ??_
	DW	@_ABRT?
	@NAM  " ?"
	DW	@_RET__

 L_ @_DXEP_
	DW @_DUP__
 L_ @_XEP__
	DW @_XCHG_,@_EXECU,@_POP__,@_RET__
	
 L_ @_TOKN?
	DW @_TOKEN,@_XZ??_
 L_ @_DLDB_				; DUP C@
	DW @_DUP__
 L_ @_LDB__
  DW @_CSTR_
 L_ @_NIP__
  DW @_PUSH_,@_DROP_,@_POP__,@_RET__
  
 L_ @_TOKEN
	DW @_BL___
 L_ @_WORD_
	DW @_PARSE,@_TBUF_
 L_ @_TOSTR	
	DW @_DXEP_,@_MKSTR,@_LIT__,'`',@_ASTBP,@_RET__
 L_ @_PARSE
	DW @_NOP__,@_ETIB_,@_LTIB_,@_PARS_,TO_+@_LTIB_,@_RET__

 L_ @_HDICT
    DW  @_HERE_,@_STR__
 L_ @_TODIC
	DW @_XDICT,@_MPUSH,@_RET__

 L_ @_@_INI
    DW  @_HDICT,@_REINI,@_EVAL_,@_REINI
    DW  @_LPAR_,@_0____,TO_+@_LTIB_,@_RET__

 L_ @_ABRT?
	DW @_@STR_,@_SWAP_,@_IF___,@_DROP_,@_ERROR,@_ABORT

 L_ @_COLON  
	DW @_ALIGN,@_RPAR_          ; :
 L_ @_HNTRY
	DW @_HERE_
 L_ @_ENTRY
	DW @_0SWAP,@_TOKN?
	DW @_XDICT,@_STRPU,@_ASTM_,@_ASTM_,@_LDAR_,@_RET__
  
; L_ @_ST___ 
;  DW @_PUSH_,@_RSTP_,@_RDROP,@_RET__
;  
; L_ @_0?X__
;  DW @_IF___,@_EXIT_,@_RET__

 L_ @_SEMI_
	DW @_SEMIC
 L_ @_LPAR_				; INTERPRETTER
	DW @_SETPT,@_?FIND,@_IF___,@_NUMBR,@_@EXEC,@_RET__

 L_ @_RPAR_		; COMPILER
	DW @_SETPT,@_?FNDC,@_DEC__,@_IF___,@RPAR2,@_@EXEC,@_RET__
@RPAR2 DW @_?FIND,@_IF___,@@COMP_NUM,@_JUMP_,@_comar
@@COMP_NUM:
	DW @_NUMBR
 L_ @_CMLIT	
	DW @_cLIT_
 L_ @_COMMA
	DW @_XHERE
 L_ @_STP__
  DW @_PUSH_
 L_ @_STPRX
  DW @_RSTP_,@_POP__,@_RET__
	
 L_ @_TICK_ ;_LOCATE  '
	DW @_XZ??_,@_TOKN?,@_JUMP_,@_?FIND
	
 L_ @_?FNDC
	DW @_DXEP_,@_J____,@_INC__
 L_ @_?FIND	
	DW @_DICT_,@_FIND_,@_RET__

 L_ @_0SWAP
  DW @_0____
 L_ @_SWAP_
  DW @_PUSH_,@_XCHG_,@_POP__,@_RET__
  
 L_ @_2DIV_
  DW @_0____
 L_ @_AVERG 
  DW @_PLAVG,@_JUMP_,@_NIP__
  
 L_ @_STRM_
  DW @_2M___,@_STR__,@_2M___,@_RET__

 L_ @_MKSTR   ; SRC LEN DST / DST
    DW @_DXEP_,@_STAR_,@_DUP__,@_ASTBP
 L_ @_AMOVE    
    DW @_PREMV,@_IF___,@CMOVE,@_PUSH_,@_CSTR_,@_ASTBP,@_POP__
@CMOVE  DW @_XTM2D,@_STR__,@_ASTP_,@_RET__
 L_ @_STRPU
    DW @_DLDB_,@_LIT__,1,@_PLUS_
    
 L_ @_MPUSH
    DW @_STAR_,@_PREMV,@_IF___,@MPUSH,@_PUSH_
    DW @_1M___,@_CSTR_,@_1M___,@_ASTBM,@_POP__
@MPUSH  DW @_XTM2D,@_STRM_,@_ASTM_,@_RET__  

 L_ @_@STR_
  DW @_J____,@_POP__,@_POP__,@_CSTR_,@_PLUS_,@_PUSH_,@_PUSH_,@_RET__
  
  
;-----------------------------------------------

freemem:
  DW ENDMEM-FREEMEM-2
  entry_  @_ENTRY,0,'=:'
  DB 0
EndMem:

; --------------  END  ---------------

 L_	@_END__

    MyCseg  ends
    end  Start

