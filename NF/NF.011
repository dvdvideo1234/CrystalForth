;._IF			;._JUMP		;._RET		;._NEXT		
;._PUSH		;._DUP		;._POP		;._J		    
;._XCHG		;._RLDP		
;_SDIV		;_PMUL		;_PLSAVG  ;_NAND	

;_EX			;_EXECUTE	;_XEP			;_XTIMES	
;_DXEP		;_@EXEC		;_2EXIT		;_XDROP		
;_@STR		;_?SKIP		;_ABORT		;_EXEC		
;_DROPX		;_EXIT		;_DRET		;_SKIP		

;!_XOR		;!_ALIGN	;_NOT			;_NEG			
;_ADD			;!_ZEQ		;_DIV2		;!_LIT			

;_RDROP		;_SWAP		;_NOP			;_DROP		


;_TIB			;_SETPNT	;_ACCEPT	;_0		    
;_BL	    ;_OK			;_TBUF		;_HERE		
;_ETIB		;_TBL			;_NUM			;_LTIB		
;_DICT		;_CNTC		

;_STR			;_STM			;_LDB			;_STP			
;_INC			;_SWAPST	;_LD	    ;_ST			
;_DEC			;_CMOVEU	;_STRPUSH	;_MAKESTR	
;_COUNT		;_MPUSH		;_CMOVE		;_DLDB		

;_FND			;_TICK		;_EVAL		;_WORD		
;_COMPNUM ;_SEMICOM	;_NUMBER	;_TOKEN?	
;_TO_STR	;_TO_DICT	;_ABORT?	;_HENTRY	
;_COMLIT	;_?FINDC	;_CLIT		;_XZ??		
;_DOIT?		;_ERROR		;_REINI		;_NZ??		
;_@NC			;_XDICT		;_XHERE		;_@EVAL		
;_Z??			;_TOKEN		;_PARSE		;_HDICT		
;_@_INI		;_COLON		;_ENTRY		;_INIT		
;_COMMA		;_?FIND		;_SEMI		;_LPAR		
;_RPAR		;_PARS		
 
 locals  @@

pd  equ dword ptr
pw  equ word  ptr
pb  equ byte  ptr

TO_    =  2
AT_    =  4
VAL_   =  6

SKIPr_   MACRO reg
	mov   reg,0
	org   $-2
    ENDM

JMP_	MACRO LBL
	jmp  SHORT LBL
	ENDM

SKIPW_	MACRO
	DB    0BFH
	ENDM

SKIPA_   MACRO
	DB    03DH
	ENDM

SKIPB_   MACRO
	DB    03CH
	ENDM

X_	MACRO
	xchg Rsp,DSP
        ENDM

CLR_	MACRO REG
  XOR   REG,REG
	ENDM

DPUSH	MACRO REG
	X_
	PUSH  REG
	X_
	ENDM

DPOP	MACRO REG
	X_
	POP	REG
	X_
	ENDM

TWICE_	macro
	DW	$+2
    endm

RDEC_    macro
	DW	@_NEXT_
	TWICE
    endm

LDP_ MACRO   REG
	XCHG	REG,AX
	LODSW
	TEST	AL,1
	XCHG	REG,AX
  ENDM

;STP MACRO   REG
;	XCHG	REG,AX
;	STOSW
;	XCHG	REG,AX
;    ENDM

ALGN_  MACRO  FLAG
    IF	($-START+FLAG) AND 1
    NOP
qqqqq = qqqqq+1
    ENDIF
    ENDM

;N_	MACRO LBL	
;LBL:
;    ENDM

L_	MACRO LBL
	ALGN_  0
LBL:
    ENDM

P_	MACRO LBL
	ALGN_  0
	NOP
LBL:
	org   $-1
  ENDM

XTSK_	macro	LBL,ADR
	ALGN_  1
	SKIPA_
	P_	LBL
	DW ADR
	ENDM
	
qqqqq = 0
ppppp = qqqqq

nam_    macro   msg
	local	endstr
	db   	endstr-$-1
	db   	msg
endstr	label   byte
	endm

ALIGN_  MACRO  FLAG
	IF ($-START+FLAG) AND 1
	NOP
	ENDIF
	ENDM

VAR_    macro   LBL,DAT
  L_ LBL
  DW      @VAR@,DAT
  ENDM

XT_     macro   LBL,ADR
  P_ LBL
  DW      ADR
  ENDM

value_  macro   lbl,dat
  ALIGN_ 0
  DW _SETVAL
  const_  lbl,dat
  endm

DEFER_  macro   lbl,dat
        L_ LBL
        DW     _JUMP,dat
        endm

vector_ macro   lbl,dat
  ALIGN_ 0
  DW _SETVAL
  DEFER_   lbl,dat
  endm

point_  macro   lbl,dat
        L_ LBL
        DW     POINT__,@SETPNT_,dat
        endm

QUAN_   MACRO   LBL,DAT
        L_ lbl
        DW     @CONS3,@SETVAR2,@VAR@,dat
        ENDM

const_  macro   lbl,dat
        L_ LBL
        DW    _CONS,DAT
        endm

entry_  macro ADR,DAT,nam
        DW    ADR,DAT
        nam_  nam
        endm


; REGISTERS
; SI INSTRUCTION POINTER
; SP RETURN STACK POINTER
; AX DATA STACK POINTER
; CX DATA STACK TOP REGISTER

; DI WORK REGISTER
; BX ADRESS REGISTER  ?
; DX BX DI - SCRATCH
; BP   - NOT USED
;


; REGISTERS     for interpretter
  I	  = SI	; INSTUCTION POINTER
  RSP = SP	; RETURN	STACK POINTER
  RT  = BP	; RETURN	STACK TOP
  DSP = AX	; DATA STACK POINTER
  
  T   = BX  ; DATA STACK TOP REGISTER
  TL  = BL  ; DATA STACK TOP REGISTER
  TH  = BH  ; DATA STACK TOP REGISTER
	A   = DI	; ADDRESS REGISTER
  WL	= CL
  WH	= CH
  W   = CX  ; WORK REGISTER
  R   = DX  ; IO & ARITH REG
	
	; CX DI SCRATCH
	; KEEP I DSP RDP RT

  NACC 	=	DI
	NBASE	=	BP

; MINIMAL SET INTERPRETTER COMMANDS
; METHOD       COMMANDS  (DOES (VAR (CONST (CONST2 (DEFER (DEFER2
;                        (SETVAR (SETVAR2 (CONST3 (SETPOINT (POINT
;                        (BARY (WARY (RW_BLK
; CONTROL      COMMANDS  (IF (JUMP (CALL (NEXT (FOR (NEST (EXEC (PERF
;                        (GOTO (RETURN (EX (XNEST (IFZ (IFM
; RETURN STACK COMMANDS  POP PUSH RDROP I J
; DATA STACK   COMMANDS  DUP DROP XCHG
; MATH         COMMANDS  +2/ LIT  ABS NEG NOT _ 1- 2- 1+ 2+
; LOGICAL      COMMANDS  XOR AND
; DATA TRANSF  COMMANDS  @R+ !R+ C!R+
; IO           COMMANDS  (IO

MyCseg  segment para  use16

        assume cs: MyCseg,  ds: MyCseg, ss: MyCseg, es: MyCseg

        org 100h

@TBUF   = -256          ; 256
@TIB    = @TBUF * 2     ; 256
@R_TOP  = @TIB * 2      ; 512  RSTACK
@S_TOP  = @R_TOP * 2    ; 1024 DSTACK
@VOCTOP = @S_TOP * 2    ; 2048 = 4 BUFFERS * 512

Start   Label byte
        JMP   SELF0

@RETUR: ; (;
  POP   A
  JMP_  @SETIP
  
; -----------------  MAIN  LOOP  ---------------

@NEST:
  PUSH  RT
  MOV   RT,I
@SETIP:  
  MOV   I,A
@MAIN:
  LDP_  A
  JE	  @NEST
  INC   A
  CALL  [A-2]
  JMP_  @MAIN

; -------------- CONTROL  -------------

@IF:
  DEC	T
@IFM1:  
  INC	T
  DPOP	T
  JE    @JUMP
@SKIP:
  INC	I
@SKIP1:
  INC	I
  RET
@NEXT:    ; (NEXT
  DEC   RT
  JS    @SKIP
@JUMP:    ; (JUMP
  MOV   I,[I]
  RET

;------------------ LOGICAL COMMANDS  XOR AND -----------------

 XT_ _XOR,@NIP@
  XOR   T,W
  RET

 XT_ _NAND,@NIP@
  AND   T,W
@NOT:
  INC	T
@NEG:
  NEG	T
  RET
  
 XT_ _NIP,@NIP@
  RET
 
;------------------ STRING COMMANDS --------------------

 XT_  _CMOVEU,@PARAM3@
  ADD   DI,CX
@MPUSH: 
  ADD   SI,CX   ; AFTER NAME ADDRESS
  STD           ; BACKWARDS
  CMPSB         ; PREPARE FOR DIRECTION
  REP   MOVSB   ; NAME  MOVE
  CLD
@PUSHB1:  
  INC   DI
@PUSHB:  
  Dpush DI
  RET

 XT_  _STRPUSH,@PARAM2@       ; S!-
  mov   Wl,[Si]
  CLR_  Wh
  INC   W
 XTSK_  _MPUSH,@PARAM3@ ; SI CX DI   ASSUME
  JMP_  @MPUSH

 XT_   _CMOVE,@PARAM3@          ; CMOVE
  XCHG  DI,CX
  JMP_  @CMOVE
  
 XT_   _MAKESTR,@PARAM3@        ; S!
  MOV   [DI],WL
  CLR_  WH
  INC   DI
@CMOVE:
  REP   MOVSB   ; NAME  MOVE
  JMP_	@PUSHB

;-------------- TRANSFER ------------------------

@XA:
  XCHG  RT,R
  RET
  
@RLDP:
  DPUSH  T
  MOV   T,DS:[RT]
@RSKIP:
  LEA   RT,[RT+@WORDSIZE]
  RET
  
@RSTP:
  MOV   DS:[RT],T
  DPOP  T
  JMP   @RSKIP
  
@NIP@:
  DPOP  W
  JMP   A
  
@RETUPLE2@:; USES & RETURNS TOP & NEXT ELEMENT OF STACK
  DPOP  W
@RETUPLE1@:   ; USES TOP | RETURNS TOP & NEXT ELEMENT OF STACK
  CALL  A
  DPUSH W
  RET
  
@PARAM2@:         
  Dpush  T
@PARAM3@:           
  PUSH  I R RT
  x_
  pop   Cx
  pop   SI
  x_
  xchg  t,a
  CALL  T
  POP   RT R I
 
;vvvvvvvvvvvvvv STACK vvvvvvvvvvvvvvvvvv

@DROP:
  DPOP  T
  RET

@XCHG:
  XCHG  T,RT
  RET
  
@DUP:
  Dpush T
  RET

@PUSH:
  POP   W
  PUSH  RT
  MOV   RT,T
  DPOP  T
  JMP   W

@POP:
  Dpush T
  POP   W
  MOV   T,RT
  POP   RT
  JMP   W

@RET:
  POP   W
  MOV   I,RT
  POP   RT
  JMP   W

@J:
  MOV   A,RSP
  Dpush SS:A[@WORDSIZE*2]
  RET
  
;^^^^^^^^^^^^^^^^ MATH & LOGIC ^^^^^^^^^^^^^^^^^^

@ZEQ:
  SUB   T,1
  SBB   T,T
  RET

@DEC:
  DEC   PW [T]
  JMP_  @DROP
    
@INC:
  INC   PW [T]
  JMP_  @DROP
    
@ALIGN:
  TEST	TL,1
  JnE	  @1p
  ret
  
@STp:
  DPOP  [T]  
@2p:  
  INC	T
@1p:  
  INC	T
  RET
  
 XT_ _CSTR,@RETUPLE1@
@CSTR:
  LEA   W,[T+1]
@LDB:  
  MOV   TL,[T]
@W_B:  
  CLR_  TH
  RET
  
 XT_ _CSTRM,@RETUPLE1@
@CSTRM:
  DEC   T
  MOV   W,T
  JMP_  @LDB 
  
@STBp:
  DPOP  W
  MOV   [T],WL
  INC	  T
  RET
  
 XT_ _SWAP,@RETUPLE2@
  JMP_  @SWAP
  
 XT_ _STRM,@RETUPLE1@
  CALL  @STRM@ 
@SWAP:
  XCHG  T,W
  RET

@STRM@:
  MOV   W,[T-@WORDSIZE]
@2M:  
  DEC   T
@1M:  
  DEC   T
  RET
  
 XT_ _STR,@RETUPLE1@
@STR:
  LEA   W,[T+@WORDSIZE]
@LD:  
  MOV   T,[T]
  RET
  
 XT_ _STBp,@STBp		; (C!+
  
 XT_ _PLAVG,@RETUPLE2@		; (+2/
@PLSAVG:  ; +2/
  ADD   W,T
  MOV   T,W
@DIV2:  
  RCR   T,1
NO_DIV:
  RET
  
 XT_ _D2DIV,@RETUPLE2@		; (D2/
@D2DIV:
  JMP_  @D2DIV_
  
 XT_ _PMUL,@RETUPLE2@		; (+*
	TEST    WL,1
	JZ      @D2DIV
	ADD     t,R
@D2DIV_:						     
	RCR     T,1
	RCR     W,1
  RET

 XT_  _SDIV,@RETUPLE2@     ; (-/
@SDIV:    ; -/
	SHL     W,1
	RCL     T,1
	CMP     T,R
	JB	    NO_DIV
	SUB     T,R
	INC     W
  RET
	
;---------------- FIND -----------------------

 XT_  _FND,@PARAM2@     ; (FIND
  XOR   CX,CX
NEXTfnd:
  PUSH  SI
  ADD	  DI,CX
  LEA   dI,[DI+4]
  mov   cl,[DI]
  Jcxz  @FND?
  INC   CX
  repe  cmpsb
  POP   SI
  JNE   NEXTFND
  mov   cL,2        ; fnd_ok
  ADD	  [DI+2],CX
  SKIPB_
@FND?:  
  POP   DI
  x_
  PUSH  DI      ; LENGTH OF THE SOURCE
  PUSH  CX      ; REST LEN OF BUF
  X_
  RET
  
;  CHAR etib ltib  /  START LEN >IN
;----------------	PARSE	-----------------
;    387
;	CHAR etib ltib	 /  START LEN >IN  / >IN
;	SI    CX	 DI   	     dX	   AX  Cx

 XT_  _PARS,@PARAM3@ ;= (PARS
  XCHG  DI,CX
  XCHG  AX,SI     ; DSP SAVE
  SUB   DI,CX
  JCXZ  @@SKIPX
  CMP   AL,' '
  JNE   @@SKIPX
  REPE  SCASB
  JE	  @@SKIPX
  DEC   DI
  INC   CX
@@SKIPX:
  MOV   DX,di     ;	START OF THE SOURCE
  JCXZ  @@WEX
  REPNE SCASB
  JNE   @@WEX
  DEC   DI
@@WEX:	          ; END OF THE SOURCE  IN DI
  XCHG  AX,SI     ; DSP RESTORE
  SUB   DI,DX     ; LENGTH OF THE WORD
  X_
  PUSH  DX
  PUSH  DI
  PUSH  CX	     ; CX REMAIN LENGTH	OF THE BUFFER
  X_
  RET

 XT_  @num,@PARAM2@      ;= (>#       ; (NUM
  push  ax        ; DSP SAVE
  MOV   AX,10     ; base := 10
  CALL  @@NUM
  pop   AX        ; DSP RESTORE
  X_
  PUSH  BX
  PUSH  CX
  X_
  RET

@@NUM:
  CALL  @@numini
  CALL  @@SGN
@@NUMZ:
  CALL  @@N1
  JMP_  @@NUMZ

@@n1:
  cmp   al,'$'     ; BASE := 16
  JNE   @@N2
  MOV   al,16
  jmp   SHORT @@setbas

@@N2:   cmp  al,'#'     ; BASE := CX
  JNE   @@N3
  mov   al,Bl
@@numini:
  Xor   Bx,Bx      ; ACCUMULATOR
@@setbas:
  mov   DI,ax
@@END?:
  JCXZ  @@ERNUM
@@GETC:
  deC   CX
  LODSB
  RET

@@N3:
  cmp   al,'9'+1
  jc    @@ton2
  AND   AL,0DFH
  cmp   al,'A'      ; no case sensivity
  jc    @@ERNUM
  sub   al,7
@@ton2:
  sub   al,'0'
  cmp   ax,DI
  jnc   @@ERNUM
  xchg  ax,Bx
  mul   DI
  xchg  ax,Bx
  add   Bx,ax
  JCXZ  @@EXNUM
  JMP   @@GETC
@@ERNUM:
  INC   CX
@@EXNUM:
  pop   dx    ; rdrop
@@noEXn:
  RET

@@SGN:
  CMP   AL,'-'     ; SGN
  JNE   @@N1
  POP   dX
  CALL  @@END?
  CALL  dX
  NEG   bX
  RET

;-----------------------------------------------------

SELF0:
  mov   bx,1000h     ; only 64k allocate
  mov   ah,4ah
  int   21h
@ABORT:
  MOV   SP,@R_TOP
  MOV   AX,@S_TOP
  CALL  @RETUR
  DW    _INIT,_INIT,_TBL,_CSTR
@MAINLP DW _EVAL,_TIB,_TBL,_ACCEPT,_OK,_JUMP,@MAINLP

  
;_OPERATION DW @WARY;
	;DW _COMP_NUM,_COMMA,_NUMBER,_EXEC

  CONST_  _TBL,80H
  CONST_  _TIB,-512
  CONST_  _TBUF,-256
  CONST_  _0,0
  CONST_  _BL,32

  VALUE_  _HERE,FREEMEM
  VALUE_  _ETIB,0
  VALUE_  _LTIB,0
  VALUE_  _DICT,@VOCTOP
  VALUE_  _CNTC,0
_@NC = $-2

  POINT_  _DOIT?,0
_SETPNT = TO_+_DOIT?


  ;DEFER_O _EMIT,_DEMIT   
  DEFER_  _OK,_nop
  DEFER_  _ACCEPT,_nop ;_ACC
  DEFER_  _NUM,@NUM
  DEFER_  _ERROR,_drop; _IDTYP

  POINT_  _INIT,_@_INI
_REINI  = TO_+_INIT

 L_ _CONS
  DW _POP,_LD,_RET
  
 L_ _SETVAL
  DW _POP,_2P,_ST,_RET
  
;------------------------------------

; @XTP _DEMIT,@2IO,200h ; dos  EMIT
;@DOSINT:
;  XCHG  AX,SI
;  XCHG  AX,DX
;  XCHG  BX,CX
;  INT   21H
;  XCHG  SI,AX
;  RET
  

@commaer@:
  call @does
@commaer:
  dw _LD,_JUMP,_comma

@MAKER:
  call @does
  dw _HENTRY,_JUMP,@commaER

 L_ _XTIMES
        DW _PUSH,_JUMP,@@LPS
@@LPS0  DW _J,_EXECUTE
@@LPS   DW _NEXT,@@LPS0
 L_ _2EXIT
	DW _RDROP
 L_ _EXIT
	DW _RDROP
 L_ _NOP	
	DW _RET
	
 L_ _XDROP	
	DW _EX
 L_ _DRET 
	DW _DROPX

 L_ _XDICT
	DW _DICT,_EX,TO_+_DICT,_RET
	
 L_ _XHERE
	DW _HERE,_EX,TO_+_HERE,_RET
	
 L_ _EVAL
	DW _NOP 
 L_ _@EVAL   
	DW _TOKEN,_DLDB,_IF,_DRET,_DOIT?,_JUMP,_@EVAL
	
 L_ _SEMICOM 
	DW @commaer@,_RET  		; (;,

 L_ _clit   
	dw @commaer@,_lit       ; (LIT,

 L_ _NUMBER
	DW _CSTR,_NUM,_NZ??,_RET
	
 L_ _XZ??
	DW _EX
 L_ _Z??
	DW _ZEQ
 L_ _NZ??
	DW	_ABORT?
	NAM_  " ?"
	DW	_RET

 L_ _DXEP
	DW _DUP
 L_ _XEP
	DW _XCHG,_EXECUTE,_POP,_RET
	
 L_ _TOKEN?
	DW _TOKEN,_XZ??
 L_ _DLDB				; DUP C@
	DW _DUP,_LDB,_RET
 L_ _TOKEN
	DW _BL
 L_ _WORD
	DW _PARSE,_TBUF
 L_ _TO_STR	
	DW _DXEP,_MAKESTR,_LIT,'`'
 L_ _SWAPST
	DW _SWAP,_ST,_RET
 L_ _PARSE
	DW _NOP,_ETIB,_LTIB,_PARS,TO_+_LTIB,_RET

 L_ _HDICT
    DW  _HERE,_STR
 L_ _TO_DICT
	DW _XDICT,_MPUSH,_RET

 L_ _@_INI
    DW  _HDICT,_REINI,_EVAL,_REINI
    DW  _LPAR,_0,TO_+_LTIB,_RET

 L_ _ABORT?
	DW _@STR,_SWAP,_IF,_DRET,_ERROR,_ABORT

 L_ _COLON  
	DW _ALIGN,_RPAR          ; :
 L_ _HENTRY
	DW _HERE
 L_ _ENTRY
	DW _0,_SWAP,_TOKEN?
	DW _XDICT,_STRPUSH,_STM,_STM,_RET

 L_ _SEMI
	DW _SEMICOM
 L_ _LPAR				; INTERPRETTER
	DW _SETPNT,_?FIND,_IF,_NUMBER,_@EXEC,_RET

 L_ _RPAR		; COMPILER
	DW _SETPNT,_?FINDC,_DEC,_IF,@RPAR2,_@EXEC,_RET
@RPAR2 DW _?FIND,_IF,_COMP_NUM,_JUMP,@commaer
 L_ _COMP_NUM
	DW _NUMBER
 L_ _COMLIT	
	DW _cLIT
 L_ _COMMA
	DW _XHERE,_STP,_RET
	
 L_ _TICK ;_LOCATE  '
	DW _XZ??,_TOKEN?,_JUMP,_?FIND
	
 L_ _?FINDC
	DW _DXEP,_J,_INC
 L_ _?FIND	
	DW _DICT,_FND,_RET
	
 L_ _LIT
	DW _RLDP,_RET
	
 XT_  _J,@J     	    ;. J
 XT_ _POP,@POP        	;. POP
 XT_ _DROP,@DROP		; DROP
 XT_ _XCHG,@XCHG		;. XCHG
   
 XT_ _NEXT,@NEXT		;. (NEXT
 XT_ _IF,@IF			; (IF 
 XT_ _JUMP,@JUMP		;. (JUMP
 XT_ _RET,@RETUR		;. (;
 XT_ _SKIP,@SKIP		; SKIP
 XT_ _ABORT,@ABORT		; ABORT
   
 XT_ _NOT,@NOT
 XT_ _NEG,@NEG
 XT_ _LD  ,0
 XT_ _PUSH,0
 XT_ _COUN,0
 XT_ _ZEQ ,0
 XT_ _DUP ,0
 XT_ _EXEC,0
 XT_ _ST  ,0
 XT_ _XDIC,0
 XT_ _@STR,0
 XT_ _ALIG,0
 XT_ _@EXE,0
 XT_ _DEC ,0
 XT_ _XHER,0
 XT_ _INC ,0
 XT_ _RLDP,0
 
 XT_ POINT__ ,0
 XT_ _2P     ,0
 XT_ @DOES   ,0
 XT_ _EXECUTE,0
 XT_ _RDROP  ,0
 XT_ _EX     ,0
 XT_ _DROPX  ,0
 XT_ _LDB    ,0
 XT_ _ALIGN  ,0
 XT_ _STM    ,0
 XT_ _@EXEC  ,0
 XT_ _STP    ,0
 XT_ @SETPNT_    ,0


;-----------------------------------------------

freemem:
  DW ENDMEM-FREEMEM-2
  ENTRY_  _ENTRY,0,'=:'
  DB 0
EndMem:


lastw = ppppp

MyCseg  ends
        end  Start

;____________________________________________
;-----------------------------------------------